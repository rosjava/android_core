/*
 * Copyright (C) 2011 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

buildscript {
    def rosMavenPath = "$System.env.ROS_MAVEN_PATH".split(':').collect { 'file://' + it }
    def rosMavenRepository = "$System.env.ROS_MAVEN_REPOSITORY"
    repositories {
        rosMavenPath.each { p ->
            maven {
                url p
            }
        }
        mavenLocal()
        maven {
            url rosMavenRepository
        }
    }
    dependencies {
        classpath group: 'org.ros.rosjava_bootstrap', name: 'gradle_plugins', version: '[0.2,0.3)'
    }
}

apply plugin: 'catkin'

allprojects {
    group 'org.ros.android_core'
    version = project.catkin.pkg.version
}

configure(subprojects.findAll {it.name.startsWith('android_')}) {
    apply plugin: 'ros-android'
    afterEvaluate { Project project ->
        // Change the layout of Android projects to be compatible with Eclipse.
        android {
            sourceSets {
                main {
                    manifest.srcFile 'AndroidManifest.xml'
                    res.srcDirs 'res'
                    assets.srcDirs 'assets'
                    java.srcDirs 'src'
                }
            }
        }

        // Copy JAR dependencies into the libs directory for Eclipse.
        task deployLibs(type: Copy) {
          from { configurations.compile }
          into { "${project.projectDir}/libs" }
        }
    }
}

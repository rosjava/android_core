/*
 * Copyright (C) 2011 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not
 * use this file except in compliance with the License. You may obtain a copy of
 * the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
 * License for the specific language governing permissions and limitations under
 * the License.
 */

task wrapper(type: Wrapper) {
    gradleVersion = '1.7'
}

buildscript {
    def rosMavenPath = "$System.env.ROS_MAVEN_PATH".split(':').collect { 'file://' + it }
    repositories {
        rosMavenPath.each { p ->
            maven {
                url p
            }
        }
        mavenLocal()
        maven {
            url 'https://github.com/rosjava/rosjava_mvn_repo/raw/master'
        }
    }
    dependencies {
        classpath group: 'org.ros.rosjava_bootstrap', name: 'gradle_plugins', version: '0.1.+'
    }
}

apply plugin: 'catkin'

allprojects {
    group 'org.ros.android_core'
    version = project.catkin.pkg.version
}

subprojects {
    if (name != 'docs') {
        apply plugin: 'maven'
        apply plugin: 'ros'
        apply plugin: 'ros-android'
        buildscript {
            repositories {
                mavenCentral()
            }
            dependencies {
                classpath 'com.android.tools.build:gradle:0.5.6'
            }
        }
        afterEvaluate { Project project ->
            /* 
             * Delayed configuration here, because we delay the decision for
             * applying android or android-library plugins until inside the sub-projects.
             */
            project.android {
                buildToolsVersion project.rosandroid.buildToolsVersion
            }
        }
    }
}
